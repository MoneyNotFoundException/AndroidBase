if (isBuildModule.toBoolean()) {
    //作为独立App应用运行
    apply plugin: "com.android.application"
} else {
    //作为组件运行
    apply plugin: "com.android.library"
}

def config = rootProject.ext.android

android {

    compileSdkVersion config.compileSdkVersion


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {

        minSdkVersion config.minSdkVersion
        targetSdkVersion config.targetSdkVersion
        versionCode config.versionCode
        versionName config.versionName

        //阿里路由框架配置
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            if (isBuildModule.toBoolean()) {
                //独立运行
                manifest.srcFile 'src/main/alone/AndroidManifest.xml'
            } else {
                //合并到宿主
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java{}

                resources {

                    //正式版本时，排除alone文件夹下所有调试文件
                    exclude 'src/main/alone/*'
                    //排除litepal文件
                    exclude 'src/main/assets/litepal.xml'
                }
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile(config.proguardAndroidTxt), config.proguardRulesPro
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile(config.proguardAndroidTxt), config.proguardRulesPro
        }
    }

    dataBinding {
        enabled true
    }

}



dependencies {

    //组件中依赖阿里路由编译框架
    annotationProcessor rootProject.ext.dependencies["arouter-compiler"]
}







